#!/usr/bin/perl
use strict;
use warnings;
use lib "$ENV{HOME}/install/bin";
use ScriptScript;

sub editConfContents($);
sub editRemoteFile($$);

my $IPMAGIC_NAME = "raspi";
my $USER = "pi";

my $CONF = "/etc/lightdm/lightdm.conf";

my $USAGE = "Usage:
  $0 -h | --help
    show this message

  $0
    setup lightdm display manager
";

sub main(@){
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  editRemoteFile($CONF, \&editConfContents);
}

sub editConfContents($){
  my ($contents) = shift;

  $contents =~ s/^#?(greeter-session)=.*$/$1=$USER-greeter/gm;
  $contents =~ s/^#?(user-session)=.*$/$1=LXDE-$USER-x/gm;
  $contents =~ s/^#?(autologin-session)=.*$/$1=LXDE-$USER-x/gm;

  $contents =~ s/^#?(autologin-user)=.*$/$1=$USER/gm;

  if($contents !~ /^autologin-user/m){
    die "ERROR: $CONF file does not contain autologin-user\n";
  }

  return $contents;
}

sub editRemoteFile($$){
  my ($remoteFile, $editSub) = @_;

  my $tmpFile = "/tmp/raspi-boot-tmp-file-" . nowMillis();
  run "ipmagic", $IPMAGIC_NAME, "-u", "root", "--scp",
    ":$remoteFile", "$tmpFile";

  if(editFile($tmpFile, $editSub)){
    run "ipmagic", $IPMAGIC_NAME, "-u", "root", "--scp",
      "$tmpFile", ":$remoteFile";

    print "\n\n===== UPDATED $remoteFile\n\n";
  }else{
    print "\n\n===== skipped $remoteFile\n\n";
  }

  run "rm", $tmpFile;
}

&main(@ARGV);

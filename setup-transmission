#!/usr/bin/perl
use strict;
use warnings;

my $origFile = "$ENV{HOME}/Code/raspi/transmission-original-settings.json";

my @dests = qw(
  /root/.config/transmission-daemon/settings.json
  /var/lib/transmission-daemon/info/settings.json
  /etc/transmission-daemon/settings.json
);

my %config = (
  'download-dir'           => '"/home/pi/Desktop/Torrents"',
  'peer-port'              => '4257',
  'rpc-password'           => '"raspberry"',
  'rpc-username'           => '"pi"',
  'rpc-whitelist-enabled'  => 'false',
  "speed-limit-up"         => '2000',
  "speed-limit-up-enabled" => 'true',
);

sub run(@);
sub install($);
sub configure();
sub getConfig();

sub main(@){
  die "Usage: $0\n" if @_ > 0;
  install "transmission-daemon";
  run "raspi", "-s", "killall transmission-daemon";
  configure();
  run "raspi", "-s", "transmission-daemon";
}

sub run(@){
  print "@_\n";
  system @_;
}

sub install($){
  my $pkg = shift;
  print "checking dpkg\n";
  my $status = `raspi -s dpkg -s $pkg 2>&1`;
  if($status !~ /^Status: install ok installed$/m){
    run "raspi -s apt-get install $pkg";
  }else{
    print "skipping install\n";
  }
}

sub configure(){
  my $host = `raspi`;
  chomp $host;
  my $tmp = "/tmp/settings.json.tmp";
  open FH, "> $tmp";
  print FH getConfig();
  close FH;
  run "scp", $tmp, "root\@$host:$tmp";
  run "rm $tmp";
  my $cmd = 'set -x; ';
  foreach my $dest(@dests){
    $cmd .= "mkdir -p `dirname $dest`; ";
    $cmd .= "rm '$dest'; ";
    $cmd .= "cp $tmp '$dest'; ";
    $cmd .= "chmod a-rwx '$dest'; ";
    $cmd .= "chmod u+rw '$dest'; ";
  }
  $cmd .= "rm $tmp; ";
  system "raspi", "-s", $cmd;
}

sub getConfig(){
  my $out = '';
  for my $line(`cat $origFile`){
    if($line =~ /^(\s*)"([^"]*)"(\s*):(\s*)([^, ]*|".*")(\s*,?\s*)\n$/){
      my ($ws1, $key, $ws2, $ws3, $val, $comma) = ($1, $2, $3, $4, $5, $6);
      if(defined $config{$key}){
        $val = $config{$key};
      }
      $line = "$ws1\"$key\"$ws2:$ws3$val$comma\n";
    }
    $out .= $line;
  }
  return $out;
}

&main(@ARGV);

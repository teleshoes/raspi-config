#!/usr/bin/perl
use strict;
use warnings;

my $ipmagicName = "raspi";
my $user = "pi";

my @HOME_RSYNC_FILES = qw(
  .bashrc
  .gitconfig
  .gpg
  .secrets
  .vimrc
  wifi.conf
  wifi-auto.conf

  .config/screen-daemon.conf
  .config/wemo-devices.conf

  .config/iguana/
  .config/qtemail/
  .config/ipmagic/
  .vim/
  resolv/
  resconfig-screens/
);

my $USRLOCALBIN_LOCAL_DIR = "$ENV{HOME}/install/root-files/usr/local/bin";
my $USRLOCALBIN_REMOTE_DIR = "/usr/local/bin";
my @USRLOCALBIN_EXECS = qw(
  alarm
  igstart
  ipmagic
  mnt
  off-cmd
  term
  pulse-enable
  pulse-vol
  vpn
  wifi-fix
);

my $HOMEBIN_LOCAL_DIR = "$ENV{HOME}/bin";
my $HOMEBIN_REMOTE_DIR = "/home/$user/bin";

my @SPECIAL_USER_RSYNCS = (
  ["$ENV{HOME}/.klomp/config-$ipmagicName", "/home/$user/.klomp/config"],
  ["$ENV{HOME}/.klomp/lib-$ipmagicName", "/home/$user/.klomp/lib"],
  ["$ENV{HOME}/Music/sheet_music/shows/sheet_1080p/", "/home/$user/Music/sheet_music/shows/sheet_1080p/"],
  ["$ENV{HOME}/Music/sheet_music/shows/music/", "/home/$user/Music/sheet_music/shows/music/"],
  ["$ENV{HOME}/Music/sheet_music/trumpet/", "/home/$user/Music/sheet_music/trumpet/"],
);
my @SPECIAL_ROOT_RSYNCS = (
  ["/usr/share/alarms/", "/usr/share/alarms/"],
  ["/usr/share/sounds/custom/", "/usr/share/sounds/custom/"],
);

my $INITIAL_USER_CMD = "
  mkdir -p /home/$user/.config/qtemail && \
  mkdir -p /home/$user/.klomp && \
  mkdir -p /home/$user/Music/sheet_music/shows && \
  mkdir -p /home/$user/Music/sheet_music/trumpet && \
  echo ok
";
my $FINAL_USER_CMD = "
  echo ok
";

my $INITIAL_ROOT_CMD = "
  mkdir -p /root/.config/qtemail && \
  if [ -L /root/bin ]; then rm -f /root/bin; fi && \
  if [ -d /root/bin ]; then rmdir /root/bin; fi && \
  ln -s /home/$user/bin/ /root/bin && \
  echo ok
";
my $FINAL_ROOT_CMD = "
  echo ok
";

sub run(@);

my $USAGE = "Usage:
  $0 -h | --help
    show this message

  $0 [OPTS]
    sync files between local home and remote with `ipmagic $ipmagicName`

  OPTS
    -q | --quick
      omit special syncs
";

sub main(@){
  my $quick = 0;
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-q|--quick)$/){
      $quick = 1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  die "Usage: $0\n" if @_ > 0;

  print "making sure $ipmagicName is ssh-able\n";
  system "ipmagic", $ipmagicName, "-s", "echo found $ipmagicName!\n";
  die "failed" if $? != 0;

  my $host = `ipmagic $ipmagicName`;
  chomp $host;

  print "\n\n===INITIAL COMMANDS\n";
  run "ipmagic", $ipmagicName, "-u", $user, $INITIAL_USER_CMD;
  run "ipmagic", $ipmagicName, "-u", "root", $INITIAL_ROOT_CMD;

  print "\n\n===RSYNC /usr/local/bin FILES\n";
  my @usrlocalbinFiles = map {"$USRLOCALBIN_LOCAL_DIR/$_"} @USRLOCALBIN_EXECS;
  run "rsync", "-avP", @usrlocalbinFiles, "root\@$host:$USRLOCALBIN_REMOTE_DIR/";

  print "\n\n===RSYNC ~/bin FILES\n";
  run "rsync", "-avP", "$HOMEBIN_LOCAL_DIR/", "$user\@$host:$HOMEBIN_REMOTE_DIR/";

  print "\n\n===RSYNC HOME FILES\n";
  for my $file(@HOME_RSYNC_FILES){
    run "rsync", "-avP", "$ENV{HOME}/$file", "$user\@$host:~/$file";
    run "rsync", "-avP", "$ENV{HOME}/$file", "root\@$host:~/$file";
  }

  if($quick){
    print "\n\n===skipping special syncs\n";
  }else{
    print "\n\n===RSYNC SPECIAL FILES\n";
    for my $rsync(@SPECIAL_USER_RSYNCS){
      my ($src, $dest) = @$rsync;
      run "rsync", "-avP", $src, "$user\@$host:$dest";
    }
    for my $rsync(@SPECIAL_ROOT_RSYNCS){
      my ($src, $dest) = @$rsync;
      run "rsync", "-avP", $src, "root\@$host:$dest";
    }
  }

  print "\n\n===FINAL COMMANDS\n";
  run "ipmagic", $ipmagicName, "-u", $user, $FINAL_USER_CMD;
  run "ipmagic", $ipmagicName, "-u", "root", $FINAL_ROOT_CMD;
}

sub run(@){
  print "@_\n";
  system @_;
  if($? != 0){
    die "ERROR: \"@_\" failed\n";
  }
}

&main(@ARGV);

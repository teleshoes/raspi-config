#!/usr/bin/perl
use strict;
use warnings;
use lib "$ENV{HOME}/install/bin";
use ScriptScript;
use File::Basename qw(basename);

my $IPMAGIC_NAME = "raspi";
my $USER = "pi";

my $LOCAL_SAVE_DIR = "$ENV{HOME}/Code/raspi/backup/cavestory-saves";
my $REMOTE_SAVE_DIR = "/home/$USER/.local/share/nxengine";

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h|--help
    show this message

  $EXEC backup
    copy remote $REMOTE_SAVE_DIR/profile*.dat to local $LOCAL_SAVE_DIR/

  $EXEC restore
    copy local $LOCAL_SAVE_DIR/* to remote $REMOTE_SAVE_DIR/
";

my $CMD_BACKUP = "backup";
my $CMD_RESTORE = "restore";

sub main(@){
  my $cmd = undef;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(backup)$/){
      $cmd = $CMD_BACKUP;
    }elsif($arg =~ /^(restore)$/){
      $cmd = $CMD_RESTORE;
    }else{
      die "ERROR: unknown arg $arg\n";
    }
  }

  die $USAGE if not defined $cmd;

  if($cmd eq $CMD_BACKUP){
    system "ipmagic", $IPMAGIC_NAME, "-u", $USER, "--rsync",
      "-avP",
      "--mkpath",
      ":$REMOTE_SAVE_DIR/*.dat",
      "$LOCAL_SAVE_DIR/latest/"
    ;
    print "\n\nbacking up new save files:\n";
    for my $file(glob "$LOCAL_SAVE_DIR/latest/*.dat"){
      my $fileType;
      $fileType = "profile1" if $file =~ /profile\.dat/i;
      $fileType = "profile$1" if $file =~ /profile(\d+)\.dat/i;
      $fileType = "settings" if $file =~ /settings\.dat/i;
      die "ERROR: unknown file $file\n" if not defined $fileType;

      my $mtime = mtime($file);
      my $mtimeFmt = `date --date \@$mtime +'%Y-%m-%d_%H%M%S'`;
      chomp $mtimeFmt;
      my $md5 = md5sum($file);
      my $fileFmt = "${mtimeFmt}_${mtime}_${fileType}_${md5}";
      my $destFile = "$LOCAL_SAVE_DIR/$fileFmt.dat";
      my @existingFiles = grep {-f $_} glob "$LOCAL_SAVE_DIR/$fileFmt*.dat";
      if(@existingFiles > 0){
        print "  skipping $fileFmt\n";
      }else{
        print "  backing up $fileFmt\n";
        run "cp", "-a", $file, $destFile
      }
    }
  }elsif($cmd eq $CMD_RESTORE){
    system "ipmagic", $IPMAGIC_NAME, "-u", $USER, "--rsync",
      "-avP",
      "$LOCAL_SAVE_DIR/latest/",
      ":$REMOTE_SAVE_DIR/",
    ;
  }else{
    die "ERROR: unknown command $cmd\n";
  }
}

&main(@ARGV);

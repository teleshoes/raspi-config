#!/usr/bin/perl
use strict;
use warnings;
use lib "$ENV{HOME}/install/bin";
use ScriptScript;

sub editConfigContents($);
sub editCmdlineContents($);
sub editRemoteFile($$);

my $IPMAGIC_NAME = "raspi";

my $FW_CONFIG_FILE = "/boot/firmware/config.txt";
my $FW_CMDLINE_FILE = "/boot/firmware/cmdline.txt";

my $WIFI_COUNTRY = "US";

my $CMDLINE_VIDEO_NAME = "HDMI-A-1";
my $MARGIN_OVERSCAN_WIDTH = 225;
my $MARGIN_OVERSCAN_HEIGHT = 120;

my $USAGE = "Usage:
  $0 -h | --help
    show this message

  $0
    edit the following files remotely on `ipmagic $IPMAGIC_NAME`:
      $FW_CONFIG_FILE
      $FW_CMDLINE_FILE
";

sub main(@){
  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  editRemoteFile($FW_CONFIG_FILE, \&editConfigContents);
  editRemoteFile($FW_CMDLINE_FILE, \&editCmdlineContents);
}

sub editConfigContents($){
  my ($contents) = shift;
  return $contents;
}

sub editCmdlineContents($){
  my ($contents) = shift;

  $contents =~ s/[\r\n]+$//;
  if($contents =~ /\n/){
    die "ERROR: multi-line cmdline $contents\n";
  }

  #remove existing wifi country from cmdline
  $contents =~ s/ cfg80211\.ieee80211_regdom=\w+( |$)/$1/;

  #remove existing margins from cmdline
  $contents =~ s/ video=$CMDLINE_VIDEO_NAME:(?:,?margin_(?:left|right|top|bottom)=\d+)*( |$)/$1/;

  if($contents =~ /\bcfg80211\.ieee80211_regdom=/){
    die "ERROR: unknown video= arg in cmdline\n$contents\n";
  }
  if($contents =~ /\bvideo=/){
    die "ERROR: unknown video= arg in cmdline\n$contents\n";
  }

  $contents .= " cfg80211.ieee80211_regdom=$WIFI_COUNTRY";

  $contents .= " video=$CMDLINE_VIDEO_NAME:"
    . "margin_left=0,"
    . "margin_right=$MARGIN_OVERSCAN_WIDTH,"
    . "margin_top=0,"
    . "margin_bottom=$MARGIN_OVERSCAN_HEIGHT"
  ;

  $contents .= "\n";

  return $contents;
}

sub editRemoteFile($$){
  my ($remoteFile, $editSub) = @_;

  my $tmpFile = "/tmp/raspi-boot-tmp-file-" . nowMillis();
  run "ipmagic", $IPMAGIC_NAME, "-u", "root", "--scp",
    ":$remoteFile", "$tmpFile";

  if(editFile($tmpFile, $editSub)){
    run "ipmagic", $IPMAGIC_NAME, "-u", "root", "--scp",
      "$tmpFile", ":$remoteFile";

    print "\n\n===== UPDATED $remoteFile\n\n";
  }else{
    print "\n\n===== skipped $remoteFile\n\n";
  }

  run "rm", $tmpFile;
}

&main(@ARGV);
